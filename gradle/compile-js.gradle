apply from: rootProject.file('gradle/node-js.gradle')

kotlin {
    targets {
        fromPreset(presets.js, 'js')
    }

    sourceSets {
        jsMain.dependencies {
            api "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutine_version"
            api "org.jetbrains.kotlinx:atomicfu-js:$atomicFU_version"
            api kotlin("stdlib-js")
        }

        jsTest.dependencies {
            api kotlin("test-js")
        }
    }
}

//must be applied AFTER source set
apply from: rootProject.file('gradle/test-mocha-js.gradle')

compileKotlinJs {
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'umd'

    kotlinOptions {
        // drop -js suffix from outputFile
        def baseName = project.name - "-js"
        outputFile = new File(outputFile.parent, baseName + ".js")
    }
}

compileTestKotlinJs {
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'umd'
}