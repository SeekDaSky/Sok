apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply from: project.rootProject.file('gradle/maven-central.gradle')

def isNative = project.name.contains("-native-")

task sourcesJar(type: Jar) {
    classifier = 'sources'
    if (!isNative) {
        from sourceSets.main.allSource
    }

    if (project.name != "sok-common") {
        // add common source, too
        from rootProject.file("common/$project.name-common/src")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            if (!isNative) {
                from components.java
            }

            artifact sourcesJar
            groupId "seekdasky.sok"
            version sok_version
            pom.withXml(configureMavenCentralMetadata)
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    override = true // for multi-platform Kotlin/Native publishing
    publications = ['maven']
    pkg {
        licenses = ['MIT']
        name = 'sok'
        repo = 'sok'
        vcsUrl = 'https://github.com/SeekDaSky/Sok.git'
        version {
            name = sok_version
            vcsTag = sok_version
        }
    }
}

// Required for K/N publishing
bintrayUpload.dependsOn publishToMavenLocal


// Required to disable publish of metadata for all but native
if (!isNative) {
    afterEvaluate {
        publishing.publications.each { pub ->
            pub.moduleDescriptorGenerator = null
        }
    }
}